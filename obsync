#!zsh

# ----------------------------------- #
# QUICK AND DIRTY OBSIDIAN THEME SYNC #
# ----------------------------------- #

# THIS SCRIPT COMES WITHOUT WARRANTY OF ANY KIND.
# USE IT AT YOUR OWN RISK.
# I ASSUME NO LIABILITY FOR THE USEFULNESS
# NOR FOR ANY SORT OF DAMAGES USING THIS SCRIPT MAY CAUSE. 

#
# This script copies an edited Obsidian theme.css file
# as well as relevant appearance and hotkey settings
# from a .obsidian "vault" folder to a "master" folder.
# It then copies these files to all Obsidian vaults.
#
# The vault identified as the "master" vault
# is the one with the last modified theme.css or hotkeys.json
#
# Tested on macOS Monterey and Obsidian 1.2.7
#

#
# IMPORTANT
#
# Script is using zsh version of "read" for macOS in order to read user input.
# If using bash, replace the following line:
#
# read "CONT?[y] to sync all vaults:"
#
# with:
# 
# read -p "[y] to sync all vaults:" CONT
#


# ----------------------------------------------------- #
# Adapt the following variables to your specific system #
# ----------------------------------------------------- #


# The folder where the current theme will be copied. Can be anywhere, including iCloud.
# This is where all vaults will be syncronised FROM.
MASTER_FOLDER="/Users/username/Library/Mobile Documents/com~apple~CloudDocs/Files/settings"

# The folder containing Obsidian vaults.
OBSIDIAN_FOLDER="/Users/username/Library/Mobile Documents/iCloud~md~obsidian/Documents"

# The theme name to be synchronized.
THEME_NAME="My Amazing Theme"

# Obsidian app .json files to be synchronised.
# Default is "appearance" and "hotkeys" only.
# We don't need to sync all .json files
# (especially not "bookmarks" which is vault-specific).
declare -a SETTINGS=("appearance" "hotkeys")


# ----------------------------------------------------- #
# Main script                                           #
# ----------------------------------------------------- #

# Colors
GREEN='\033[;32m'
RESET='\033[0m'

# Find the most recently modified theme.css or hotkeys.json file in the Obsidian folder
LAST_MODIFIED=$(find $OBSIDIAN_FOLDER -type f \( -name theme.css -o -name hotkeys.json \) -print0 | xargs -0 stat -f "%m %N" | sort -rn | head -1)

# Extract the time column and the filename column, with delimiter=space
LAST_MODIFIED_TIME=$(echo $LAST_MODIFIED | cut -f1 -d" ")
LAST_MODIFIED_FILE=$(echo $LAST_MODIFIED | cut -f2- -d" ")

# Find the most recently modified theme.css or hotkeys.json file in the master folder
# and extract its time column
LAST_MODIFIED_MASTER_TIME=$(find $MASTER_FOLDER -type f \( -name theme.css -o -name hotkeys.json \) -print0 | xargs -0 stat -f "%m %N" | sort -rn | head -1 | cut -f1 -d" ")

# If the most recent vault's last modified time is less then or equal to the master's one
# we don't need to sync.
if [[ $(($LAST_MODIFIED_TIME)) -le $(($LAST_MODIFIED_MASTER_TIME)) ]] ; then
  echo "No vault has been modified since last sync. Aborting."
  exit
fi

# Extract the vault name from the file path
# /.*$ matches everything after first slash
VAULT_NAME=$(echo $LAST_MODIFIED_FILE | sed -e "s+^$OBSIDIAN_FOLDER/++" -e "s+/.*$++")


# Get user confirmation
echo "--------------------------------"
echo "Obsidian theme and settings sync"
echo "--------------------------------"
echo "Last modified file is:"
echo "  $LAST_MODIFIED_FILE"
echo "Latest vault is $GREEN$VAULT_NAME$RESET"
read "CONT?[y] to sync all vaults:"

# Sync all the necessary files
if ! [ "$CONT" = "y" ]; then

  # Abort if not "y"
  echo Aborting.
  exit

else

  echo $MASTER_FOLDER/$THEME_NAME/

  # Copy theme.css to the master folder
  if cp $LAST_MODIFIED_FILE "$MASTER_FOLDER/$THEME_NAME/" ; then
    echo "\ntheme.css -> master"
  else
    echo "\n*** Theme copy error. Is '$THEME_NAME' in the 'themes' folder?"
    exit
  fi

  # Copy .json settings to master folder
  for SETTING in $SETTINGS; do
    if ! cp "$OBSIDIAN_FOLDER/$VAULT_NAME/.obsidian/$SETTING.json" "$MASTER_FOLDER/$THEME_NAME/" ; then
      echo "*** .json settings copy error ($SETTING)."
      exit
    else
      echo "$SETTING.json -> master"
    fi
  done

  echo "$GREEN-- Master copy OK\n$RESET"

  # Iterate over all directories (Obsidian "vaults")
  for FILE in "$OBSIDIAN_FOLDER"/*/; do

    echo "Vault: $FILE"

    # Copy theme.css from the master folder to each vault
    cp "$MASTER_FOLDER/$THEME_NAME/theme.css" "$FILE.obsidian/themes/$THEME_NAME/"
    echo "  master theme.css -> vault"

    # Copy .json settings from the master folder to each vault
    for SETTING in $SETTINGS; do
      cp "$MASTER_FOLDER/$THEME_NAME/$SETTING.json" "$FILE.obsidian/"
      echo "  master $SETTING.json -> vault"
    done
  done

  echo "$GREEN-- Obsidian vaults copy OK\n$RESET"

fi

echo "Done."